if (DEFINED CMAKE_NINTENDO_SWITCH_SDK)
    set(CMAKE_NINTENDO_SWITCH_SDK "${CMAKE_NINTENDO_SWITCH_SDK}" CACHE INTERNAL "")
endif()

if (NOT EXISTS "${CMAKE_NINTENDO_SWITCH_SDK}/NintendoSdkRootMark")
    message(FATAL_ERROR "Cannot found switch sdk(${CMAKE_NINTENDO_SWITCH_SDK})")
endif()

if (NOT DEFINED CMAKE_SYSTEM_PROCESSOR OR CMAKE_SYSTEM_PROCESSOR STREQUAL "")
    set(CMAKE_SYSTEM_PROCESSOR "aarch64")
endif()

if (CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64")
    set(CMAKE_NINTENDO_SWITCH_ARCH aarch64)
    set(CMAKE_NS_TARGET "aarch64-nintendo-nx-nncfi")
    set(CMAKE_NS_PROFILE "NX-NXFP2-a64")
    set(CMAKE_SIZEOF_VOID_P 8)
elseif (CMAKE_SYSTEM_PROCESSOR STREQUAL "armv7l")
    set(CMAKE_NINTENDO_SWITCH_ARCH armv7l)
    set(CMAKE_NS_TARGET "armv7l-nintendo-nx-eabihf")
    set(CMAKE_NS_PROFILE "NX-NXFP2-a32")
    set(CMAKE_SIZEOF_VOID_P 4)
else()
    message(FATAL_ERROR "")
endif()

include_directories(SYSTEM "${CMAKE_NINTENDO_SWITCH_SDK}/Include" "${CMAKE_NINTENDO_SWITCH_SDK}/Common/Configs/Targets/${CMAKE_NS_PROFILE}/Include")

if(CMAKE_HOST_SYSTEM_NAME STREQUAL Windows)
	set(CMAKE_NINTENDO_SWITCH_EXE_SUFFIX .exe)
endif()

set(CMAKE_CROSSCOMPILING on)

set(CMAKE_STATIC_LIBRARY_PREFIX "lib")
set(CMAKE_STATIC_LIBRARY_SUFFIX ".a")
set(CMAKE_SHARED_LIBRARY_PREFIX "")
set(CMAKE_SHARED_LIBRARY_SUFFIX ".nss")

set(CMAKE_FIND_LIBRARY_SUFFIXES ".nss" ".a")

set(CMAKE_NS_SYSROOT "${CMAKE_NINTENDO_SWITCH_SDK}/Compilers/NX/nx/${CMAKE_NINTENDO_SWITCH_ARCH}")

set(CMAKE_NS_LIBRARIES_DEBUG "${CMAKE_NINTENDO_SWITCH_SDK}/Libraries/${CMAKE_NS_PROFILE}/Develop")
set(CMAKE_NS_LIBRARIES_RELEASE "${CMAKE_NINTENDO_SWITCH_SDK}/Libraries/${CMAKE_NS_PROFILE}/Release")

set(CMAKE_NS_LINK_LIBS_RELEASE "${CMAKE_NS_LIBRARIES_RELEASE}/nnSdk.nss ${CMAKE_NS_LIBRARIES_RELEASE}/nnrtld.nss ${CMAKE_NS_LIBRARIES_RELEASE}/rocrt.o ${CMAKE_NS_LIBRARIES_RELEASE}/nnApplication.o ${CMAKE_NS_LIBRARIES_RELEASE}/libnn_init_memory.a")
set(CMAKE_EXE_LINKER_FLAGS_RELEASE_INIT "${CMAKE_NS_LINK_LIBS_RELEASE} -lshim -lc ${CMAKE_NS_LIBRARIES_RELEASE}/crtend.o")
set(CMAKE_SHARED_LINKER_FLAGS_RELEASE_INIT "${CMAKE_NS_LINK_LIBS_RELEASE} -lshim -lc")

set(CMAKE_NS_LINK_LIBS_DEBUG "${CMAKE_NS_LIBRARIES_DEBUG}/nnSdk.nss ${CMAKE_NS_LIBRARIES_DEBUG}/nnrtld.nss ${CMAKE_NS_LIBRARIES_DEBUG}/rocrt.o ${CMAKE_NS_LIBRARIES_DEBUG}/nnApplication.o ${CMAKE_NS_LIBRARIES_DEBUG}/libnn_init_memory.a")
set(CMAKE_EXE_LINKER_FLAGS_DEBUG_INIT "${CMAKE_NS_LINK_LIBS_DEBUG} -lshim -lc ${CMAKE_NS_LIBRARIES_DEBUG}/crtend.o")
set(CMAKE_SHARED_LINKER_FLAGS_DEBUG_INIT "${CMAKE_NS_LINK_LIBS_DEBUG} -lshim -lc")

set(CMAKE_CXX_STANDARD_COMPUTED_DEFAULT 14)
set(CMAKE_C_STANDARD_COMPUTED_DEFAULT 11)

set(CMAKE_C_FLAGS_INIT "-DNN_NINTENDO_SDK")
set(CMAKE_CXX_FLAGS_INIT "-DNN_NINTENDO_SDK")
set(CMAKE_ASM_FLAGS_INIT "-DNN_NINTENDO_SDK")

set(CMAKE_C_FLAGS_RELEASE_INIT "-DNN_SDK_BUILD_RELEASE")
set(CMAKE_C_FLAGS_DEBUG_INIT "-DNN_SDK_BUILD_DEBUG")
set(CMAKE_CXX_FLAGS_RELEASE_INIT "-DNN_SDK_BUILD_RELEASE")
set(CMAKE_CXX_FLAGS_DEBUG_INIT "-DNN_SDK_BUILD_DEBUG")
set(CMAKE_ASM_FLAGS_RELEASE_INIT "-DNN_SDK_BUILD_RELEASE")
set(CMAKE_ASM_FLAGS_DEBUG_INIT "-DNN_SDK_BUILD_DEBUG")

set(CMAKE_C_COMPILER_TARGET ${CMAKE_NS_TARGET})
set(CMAKE_CXX_COMPILER_TARGET ${CMAKE_NS_TARGET})
set(CMAKE_ASM_COMPILER_TARGET ${CMAKE_NS_TARGET})

set(CMAKE_C_COMPILER ${CMAKE_NINTENDO_SWITCH_SDK}/Compilers/NX/nx/${CMAKE_NINTENDO_SWITCH_ARCH}/bin/clang${CMAKE_NINTENDO_SWITCH_EXE_SUFFIX})
set(CMAKE_CXX_COMPILER ${CMAKE_NINTENDO_SWITCH_SDK}/Compilers/NX/nx/${CMAKE_NINTENDO_SWITCH_ARCH}/bin/clang++${CMAKE_NINTENDO_SWITCH_EXE_SUFFIX})
set(CMAKE_ASM_COMPILER ${CMAKE_NINTENDO_SWITCH_SDK}/Compilers/NX/nx/${CMAKE_NINTENDO_SWITCH_ARCH}/bin/clang${CMAKE_NINTENDO_SWITCH_EXE_SUFFIX})
set(CMAKE_ASM_COMPILER_ID Clang)

set(CMAKE_AR                                ${CMAKE_NINTENDO_SWITCH_SDK}/Compilers/NX/nx/${CMAKE_NINTENDO_SWITCH_ARCH}/bin/${CMAKE_NINTENDO_SWITCH_ARCH}-nintendo-nx-elf-ar${CMAKE_NINTENDO_SWITCH_EXE_SUFFIX} CACHE STRING "")
set(CMAKE_RANLIB                            ${CMAKE_NINTENDO_SWITCH_SDK}/Compilers/NX/nx/${CMAKE_NINTENDO_SWITCH_ARCH}/bin/${CMAKE_NINTENDO_SWITCH_ARCH}-nintendo-nx-elf-ranlib${CMAKE_NINTENDO_SWITCH_EXE_SUFFIX} CACHE STRING "")

set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)
set(CMAKE_TRY_COMPILE_PLATFORM_VARIABLES CMAKE_NINTENDO_SWITCH_SDK)
if (DEFINED CMAKE_TIZEN_SDK)
	set(CMAKE_TIZEN_SDK ${CMAKE_TIZEN_SDK} CACHE INTERNAL "")
endif()

if (NOT EXISTS "${CMAKE_TIZEN_SDK}/sdk.version")
	message(FATAL_ERROR "Cannot find Tizen SDK(\${CMAKE_TIZEN_SDK})")
endif()

if (NOT DEFINED CMAKE_TIZEN_BUILD_TOOL)
	set(CMAKE_TIZEN_BUILD_TOOL clang)
endif()

if (CMAKE_TIZEN_BUILD_TOOL STREQUAL "clang" OR CMAKE_TIZEN_BUILD_TOOL STREQUAL "gcc")
else()
	message(FATAL_ERROR "Unknown build tool")
	return()
endif()

if (NOT DEFINED CMAKE_TIZEN_SDK_VERSION)
	set(CMAKE_TIZEN_SDK_VERSION "4.0")
endif()

if (NOT DEFINED CMAKE_TIZEN_SDK_PLATFORM)
	set(CMAKE_TIZEN_SDK_PLATFORM "Mobile")
endif()

string(TOLOWER ${CMAKE_TIZEN_SDK_PLATFORM} _CMAKE_TIZEN_SDK_PLATFORM)
if (_CMAKE_TIZEN_SDK_PLATFORM STREQUAL "tv")
	message(FATAL_ERROR "You can build tv application with web only")
elseif (_CMAKE_TIZEN_SDK_PLATFORM STREQUAL "mobile" OR _CMAKE_TIZEN_SDK_PLATFORM STREQUAL "wearable")
else()
	message(FATAL_ERROR "Unknown platform")
endif()

if (NOT DEFINED CMAKE_SYSTEM_PROCESSOR OR CMAKE_SYSTEM_PROCESSOR STREQUAL "")
	set(CMAKE_SYSTEM_PROCESSOR arm)
endif()

if (CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64" OR CMAKE_SYSTEM_PROCESSOR STREQUAL "arm")
	set(CMAKE_TIZEN_EMULATOR off)
elseif (CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64" OR CMAKE_SYSTEM_PROCESSOR STREQUAL "i586" OR CMAKE_SYSTEM_PROCESSOR STREQUAL "i386")
	set(CMAKE_TIZEN_EMULATOR on)
else()
	message(FATAL_ERROR "Unsupported CMAKE_SYSTEM_PROCESSOR ${CMAKE_SYSTEM_PROCESSOR}")
endif()

if (CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64")
	set(_CMAKE_TIZEN_PROFILE "device64")
	set(CMAKE_TIZEN_TRIPLE "aarch64-linux-gnu")
	set(CMAKE_TIZEN_TARGET "aarch64-tizen-linux-gnu")
elseif (CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64")
	set(_CMAKE_TIZEN_PROFILE "emulator64")
	set(CMAKE_TIZEN_TRIPLE "x86_64-linux-gnu")
	set(CMAKE_TIZEN_TARGET "x86_64-tizen-linux-gnu")
elseif (CMAKE_SYSTEM_PROCESSOR STREQUAL "i586")
	set(_CMAKE_TIZEN_PROFILE "emulator")
	set(CMAKE_TIZEN_TRIPLE "i586-linux-gnueabi")
	set(CMAKE_TIZEN_TARGET "i586-tizen-linux-gnueabi")
elseif (CMAKE_SYSTEM_PROCESSOR STREQUAL "i386")
	set(_CMAKE_TIZEN_PROFILE "emulator")
	set(CMAKE_TIZEN_TRIPLE "i386-linux-gnu")
	set(CMAKE_TIZEN_TARGET "i386-tizen-linux-gnu")
elseif (CMAKE_SYSTEM_PROCESSOR STREQUAL "arm")
	set(_CMAKE_TIZEN_PROFILE "device")
	set(CMAKE_TIZEN_TRIPLE "arm-linux-gnueabi")
	set(CMAKE_TIZEN_TARGET "arm-tizen-linux-gnueabi")
endif()

set(CMAKE_TIZEN_SYSROOT "${CMAKE_TIZEN_SDK}/platforms/tizen-${CMAKE_TIZEN_SDK_VERSION}/${_CMAKE_TIZEN_SDK_PLATFORM}/rootstraps/${_CMAKE_TIZEN_SDK_PLATFORM}-${CMAKE_TIZEN_SDK_VERSION}-${_CMAKE_TIZEN_PROFILE}.core")

if (NOT EXISTS ${CMAKE_TIZEN_SYSROOT})
	message(FATAL_ERROR "Cannot find Tizen SDK at ${CMAKE_TIZEN_SYSROOT}")
endif()

if(CMAKE_HOST_SYSTEM_NAME STREQUAL Windows)
	set(CMAKE_TIZEN_EXE_SUFFIX .exe)
endif()

set(CMAKE_CROSSCOMPILING on)
set(CMAKE_SYSROOT "${CMAKE_TIZEN_SYSROOT}")
set(CMAKE_FIND_ROOT_PATH "${CMAKE_TIZEN_SYSROOT}")

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

set(TIZEN_COMPILER_FLAGS "-D__TIZEN__")

if (CMAKE_TIZEN_SDK)
	file(GLOB toolchains RELATIVE "${CMAKE_TIZEN_SDK}/tools" "${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}*")
	set(CMAKE_TIZEN_GCC_VERSION 0.0)
	foreach(dir ${toolchains})
		string(REPLACE "${CMAKE_TIZEN_TRIPLE}-gcc-" "" toolchainver ${dir})
		if (toolchainver VERSION_GREATER CMAKE_TIZEN_GCC_VERSION)
			set(CMAKE_TIZEN_GCC_VERSION ${toolchainver})
		endif()
	endforeach()
endif()

set(CMAKE_AR			${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION}/bin/${CMAKE_TIZEN_TRIPLE}-ar${CMAKE_TIZEN_EXE_SUFFIX} CACHE STRING "ar")
set(CMAKE_STRIP			${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION}/bin/${CMAKE_TIZEN_TRIPLE}-strip${CMAKE_TIZEN_EXE_SUFFIX} CACHE STRING "strip")
set(CMAKE_RANLIB		${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION}/bin/${CMAKE_TIZEN_TRIPLE}-ranlib${CMAKE_TIZEN_EXE_SUFFIX} CACHE STRING "ranlib")

if (CMAKE_TIZEN_BUILD_TOOL STREQUAL "gcc")
	set(CMAKE_C_COMPILER_ID                     GNU)
	set(CMAKE_CXX_COMPILER_ID                   GNU)
	set(CMAKE_ASM_COMPILER_ID                   GNU)

	set(CMAKE_C_FLAGS_INIT 						"${TIZEN_COMPILER_FLAGS}")
	set(CMAKE_CXX_FLAGS_INIT 					"${TIZEN_COMPILER_FLAGS}")
	set(CMAKE_ASM_FLAGS_INIT 					"${TIZEN_COMPILER_FLAGS}")

	set(CMAKE_C_COMPILER						${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION}/bin/${CMAKE_TIZEN_TRIPLE}-gcc${CMAKE_TIZEN_EXE_SUFFIX})
	set(CMAKE_CXX_COMPILER						${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION}/bin/${CMAKE_TIZEN_TRIPLE}-g++${CMAKE_TIZEN_EXE_SUFFIX})
	set(CMAKE_ASM_COMPILER						${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION}/bin/${CMAKE_TIZEN_TRIPLE}-gcc${CMAKE_TIZEN_EXE_SUFFIX})
elseif(CMAKE_TIZEN_BUILD_TOOL STREQUAL "clang")
	set(CMAKE_C_FLAGS_INIT 						"${TIZEN_COMPILER_FLAGS} -ccc-gcc-name ${CMAKE_TIZEN_TRIPLE}-g++")
	set(CMAKE_CXX_FLAGS_INIT 					"${TIZEN_COMPILER_FLAGS} -ccc-gcc-name ${CMAKE_TIZEN_TRIPLE}-g++")
	set(CMAKE_ASM_FLAGS_INIT 					"${TIZEN_COMPILER_FLAGS} -ccc-gcc-name ${CMAKE_TIZEN_TRIPLE}-g++")

	set(CMAKE_C_COMPILER_ID                     Clang)
	set(CMAKE_CXX_COMPILER_ID                   Clang)
	set(CMAKE_ASM_COMPILER_ID                   Clang)
	
	file(GLOB toolchains RELATIVE "${CMAKE_TIZEN_SDK}/tools" "${CMAKE_TIZEN_SDK}/tools/llvm-*")
	set(CMAKE_TIZEN_CLANG_VERSION 0.0)
	foreach(dir ${toolchains})
		string(REPLACE "llvm-" "" toolchainver ${dir})
		if (toolchainver VERSION_GREATER CMAKE_TIZEN_CLANG_VERSION)
			set(CMAKE_TIZEN_CLANG_VERSION ${toolchainver})
		endif()
	endforeach()

	set(CMAKE_C_COMPILER_TARGET					${CMAKE_TIZEN_TARGET} CACHE INTERNAL "")
	set(CMAKE_C_COMPILER_EXTERNAL_TOOLCHAIN		${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION} CACHE INTERNAL "")
	set(CMAKE_C_COMPILER						${CMAKE_TIZEN_SDK}/tools/llvm-${CMAKE_TIZEN_CLANG_VERSION}/bin/clang${CMAKE_TIZEN_EXE_SUFFIX})

	set(CMAKE_CXX_COMPILER_TARGET				${CMAKE_TIZEN_TARGET} CACHE INTERNAL "")
	set(CMAKE_CXX_COMPILER_EXTERNAL_TOOLCHAIN	${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION} CACHE INTERNAL "")
	set(CMAKE_CXX_COMPILER						${CMAKE_TIZEN_SDK}/tools/llvm-${CMAKE_TIZEN_CLANG_VERSION}/bin/clang++${CMAKE_TIZEN_EXE_SUFFIX})

	set(CMAKE_ASM_COMPILER_TARGET				${CMAKE_TIZEN_TARGET} CACHE INTERNAL "")
	set(CMAKE_ASM_COMPILER_EXTERNAL_TOOLCHAIN	${CMAKE_TIZEN_SDK}/tools/${CMAKE_TIZEN_TRIPLE}-gcc-${CMAKE_TIZEN_GCC_VERSION} CACHE INTERNAL "")
	set(CMAKE_ASM_COMPILER						${CMAKE_TIZEN_SDK}/tools/llvm-${CMAKE_TIZEN_CLANG_VERSION}/bin/clang${CMAKE_TIZEN_EXE_SUFFIX})
endif()

set(CMAKE_TRY_COMPILE_PLATFORM_VARIABLES CMAKE_TIZEN_SDK
	CMAKE_TIZEN_BUILD_TOOL CMAKE_TIZEN_SDK_VERSION CMAKE_TIZEN_SDK_PLATFORM)